// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Inc16(in=val, out=incin);
    Mux8Way16(         // RIL
              a=val,   // 000
              b=in,    // 001
              c=incin, // 010
              d=in,    // 011
              e=false, // 100
              f=false, // 101
              g=false, // 110
              h=false, // 111
              sel[0]=load, sel[1]=inc, sel[2]=reset, out=adjustedin);
    Register(in=adjustedin, load=true, out=val, out=out);
}
